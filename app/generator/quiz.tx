Test:
    "Start" "test"
    title = STRING
    type = Type
    "End" "test"
;

Type:
    Quiz | Survey
;

Quiz:
    "Start quiz"
    questions += QuizQuestion
    button = Button
    "End quiz"
;

QuizQuestion:
    question = Question
    multipleAnswers = AnswerType
    answers += QuizAnswer
;

AnswerType:
    'one answer' | 'multiple answers'
;

QuizAnswer:
    (( answer = Answer isCorrect = Correct) | answer = OrderedAnswer | answer = MatchingAnswer)
;

Correct:
    '+' | '-'
;

Answer:
    "Answer"
    tag = STRING
    text = STRING
;


OrderedAnswer:
    "Answer"
    order = "ordered"
    sortedAnswers += STRING
;

MatchingAnswer:
    "Answer"
    matching = "matching"
    matchedAnswers += Matches
;

Matches:
    key = STRING
    value = STRING
    ';'
;

Button:
    "button" name = ID label = STRING
;

Survey:
    "Start survey"
    questions += SurveyQuestion
    button = Button
    "End survey"
;

SurveyQuestion:
    question = Question
    questionType = QuestionType
;

Question:
    "Question"
    ordinal = INT
    text = STRING
;

QuestionType:
    MultipleChoice | LikertType | SemanticDifferential | RankOrder | OpenEnded
;

QuestionLabel:
    'Likert' | 'SemanticDiff' | 'RankOrder' | 'OpenEnded'
;

MultipleChoice:
    'multiple choice'
    answerType = AnswerType
    answers += Answer
;

LikertType:
    'Likert'
    label = QuestionLabel
    answers += LikertAnswer
    statements += LikertStatement
    presentationType = PresentationType
;

LikertAnswer:
    'Answer'
    number = INT
    description = STRING
;

LikertStatement:
    'Statement'
    text = STRING
;

SemanticDifferential:
    'Semantic dif'
    label = QuestionLabel
    answers += SemanticDifAnswer
    presentationType = PresentationType
;

PresentationType:
    'slider' | 'radio group'
;

SemanticDifAnswer:
    'Answer left'
    leftSideAnswer = STRING
    leftValue = INT
    'Answer right'
    rightSideAnswer = STRING
    rightValue = INT
;

RankOrder:
    'Rank order'
    label = QuestionLabel
    answers += AnswerRank
;

AnswerRank:
    'Answer'
    text = STRING
;

OpenEnded:
    'Open ended'
    label = QuestionLabel
    presentationType = PresentationTypeOpen
;

PresentationTypeOpen:
    'text area' | 'input'
;
